# Maintainer: PitBall

pkgbase=libx32-clang
#pkgname=('libx32-llvm'
#        'libx32-clang')
pkgname=libx32-clang
pkgver=3.4
pkgrel=1
pkgdesc="Low Level Virtual Machine (x32 ABI)"
arch=('x86_64')
url="http://llvm.org/"
license=('custom')
oldver=$( echo $pkgver - 0.1 | bc )
newver=$( echo $pkgver + 0.1 | bc )
makedepends=('libx32-libffi' 'libx32-zlib' 'python2' 'gcc-multilib-x32' 'clang' 'libx32-llvm' 'bc')
source=(http://llvm.org/releases/$pkgver/llvm-$pkgver.src.tar.gz
        http://llvm.org/releases/$pkgver/clang-$pkgver.src.tar.gz
        http://llvm.org/releases/$pkgver/compiler-rt-$pkgver.src.tar.gz)
sha256sums=('25a5612d692c48481b9b397e2b55f4870e447966d66c96d655241702d44a2628'
            '22a9780db3b85a7f2eb9ea1f7f6e00da0249e3d12851e8dea0f62f1783242b1b'
            'f37c89b1383ce462d47537a0245ac798600887a9be9f63073e16b79ed536ab5c')
options=('staticlibs')

prepare() {
  cd "$srcdir/llvm-$pkgver"

  rm -rf projects/compiler-rt
  mv "$srcdir/clang-$pkgver" tools/clang

  rm -rf projects/compiler-rt
  mv "$srcdir/compiler-rt-$pkgver" projects/compiler-rt

  # Fix installation directories, ./configure doesn't seem to set them right
  sed -i -e 's:\$(PROJ_prefix)/lib:$(PROJ_prefix)/libx32:' \
         -e 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
    Makefile.config.in
  sed -i '/ActiveLibDir = ActivePrefix/s:lib:libx32:' \
    tools/llvm-config/llvm-config.cpp
  sed -i 's:LLVM_LIBDIR="${prefix}/lib":LLVM_LIBDIR="${prefix}/libx32":' \
    autoconf/configure.ac \
    configure

  # Fix insecure rpath (http://bugs.archlinux.org/task/14017)
  sed -i 's:$(RPATH) -Wl,$(\(ToolDir\|LibDir\|ExmplDir\))::g' Makefile.rules

  # Use system libx32-llvm
  install -d $srcdir/llvm-$pkgver/Release/lib
  for file in ` pacman -Ql libx32-llvm |grep /libx32/ |awk '{print $2}' |sed '/\/$/d'`; do
  ln -sf $file $srcdir/llvm-$pkgver/Release/lib/
  done
}

build() {
  cd "$srcdir/llvm-$pkgver"
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export CLANG="clang -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"
  export LLVM_CONFIG=/usr/bin/llvm-configx32

  # Include location of libffi headers in CPPFLAGS
  CPPFLAGS+=" $(pkg-config --cflags libffi)"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/libx32 \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=host \
    --enable-experimental-targets=R600 \
    --enable-bindings=none \
    --disable-expensive-checks \
    --disable-debug-runtime \
    --disable-assertions \
    --with-binutils-include=/usr/include \
    --with-python=/usr/bin/python2 \
    --host=x86_64-unknown-linux-gnux32 \
    --target=x86_64-unknown-linux-gnux32 \
    --enable-optimized

#    --disable-clang-static-analyzer \

#  make
  make -C projects/compiler-rt clang_linux
  make -C tools/clang/utils/TableGen
  make -C tools/clang/lib
}

#package_lib32-llvm() {
#  pkgdesc="Low Level Virtual Machine (32-bit)"
#  depends=('lib32-libffi' 'lib32-zlib' 'gcc-multilib' 'llvm')
#
#  cd "$srcdir/llvm-$pkgver"
#
#  # We move the clang directory out of the tree so it won't get installed and
#  # then we bring it back in for the clang package
#  mv tools/clang "$srcdir"
#
#  make -j1 DESTDIR="$pkgdir" install
#  mv "$srcdir/clang" tools
#
#  # Fix permissions of static libs
#  chmod -x "$pkgdir"/usr/lib32/*.a
#
#  # Get rid of example Hello transformation
#  rm "$pkgdir"/usr/lib32/*LLVMHello.*
#  rm -rf "$pkgdir"/usr/{lib,include,share}
#
#  install -d "$pkgdir/usr/lib32/bfd-plugins"
#  ln -s ../LLVMgold.so "$pkgdir/usr/lib32/bfd-plugins/LLVMgold.so"
#
#  install -d "$pkgdir/usr/include/llvm/Config"
#  install -m644 $srcdir/llvm-$pkgver/include/llvm/Config/llvm-config.h \
#  "$pkgdir/usr/include/llvm/Config/llvm-config-32.h"
#  install -m644 $srcdir/llvm-$pkgver/include/llvm/Config/config.h \
#  "$pkgdir/usr/include/llvm/Config/config-32.h"
#
#  ln -sf i686-pc-linux-gnu-llvm-config $pkgdir/usr/bin/llvm-config-32
#  ln -sf i686-pc-linux-gnu-llc $pkgdir/usr/bin/llc-32
#}

package_libx32-clang() {
  pkgdesc="CLang x32 ABI static libraries"
  url="http://clang.llvm.org/"
  depends=("clang>$oldver" "clang<$newver" "libx32-llvm>$oldver" "libx32-llvm<$newver" 'gcc-multilib-x32')

  install -d $pkgdir/usr/libx32/clang/$pkgver
  ln -s libx32 $pkgdir/usr/lib

  cd "$srcdir/llvm-$pkgver/tools/clang"
  make -C lib DESTDIR="$pkgdir" install
  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/libx32/*.a
  mv $pkgdir/usr/libx32/clang{,/$pkgver}/include
  rm -rf $pkgdir/usr/lib
}
